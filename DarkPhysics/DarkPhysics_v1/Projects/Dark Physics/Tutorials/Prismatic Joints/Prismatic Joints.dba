
` demonstrates how to use prismatic joints

` set up program
phy start
autocam off
sync on
sync rate 60
color backdrop 0
position camera 0, 20, -70
make light 1
set directional light 1, -5, -5, 5

` create ground object
make object box 1, 50, 1, 50
color object 1, rgb ( rnd ( 255 ), rnd ( 255 ), rnd ( 255 ) )
phy make rigid body static box 1

` set up dynamic boxes
y = 5

for i = 2 to 3
   make object cube i, 10
   position object i, 0, y, 0
   color object i, rgb ( rnd ( 255 ), rnd ( 255 ), rnd ( 255 ) )
   set object specular i, rgb ( rnd ( 255 ), rnd ( 255 ), rnd ( 255 ) )
   set object ambient i, 255
   set object specular power i, 255
   phy make rigid body dynamic box i
   y = y + 24
next i

` hook boxes together with primsatic joint
phy make prismatic joint 1, 2, 3, object position x ( 2 ), object position y ( 2 ), object position z ( 2 ), 0, 5, 0

` display Dark Physics logo
load image "logo.png", 100000
sprite 1, 0, 600 - 60, 100000

` main program loop
do
   ` display instructions
   set cursor 0, 0
   print "Prismatic joint example"
   print "Press the space bar to apply linear velocity to the joint"

   ` apply velocity when space key is pressed
   if spacekey ( )
      phy set rigid body linear velocity 3, 0, 10, 0
   endif

   ` update simulation and screen
   phy update
   sync
loop


